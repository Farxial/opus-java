plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
}

group = 'club.minnced'
project.version = '1.0.1'
javadoc.enabled = false

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    implementation 'net.java.dev.jna:jna:4.4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/discord-java/opus-java.git'
        githubRepo = 'discord-java/opus-java'
        publish = true
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}


String getProjectProperty(String propertyName) {
    return hasProperty(propertyName) ? this.properties[propertyName] : ""
}

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.6"
}
